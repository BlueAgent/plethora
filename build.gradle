project.ext.enabled_integration = subprojects.findAll{it.path.startsWith(':plethora-integration:') }.toList()

allprojects {
	group = 'org.squiddev'
	version = "${project.mc_version}-${project.mod_version}"

	gradle.projectsEvaluated {
		tasks.withType(JavaCompile) {
			options.compilerArgs << "-Xlint" << "-Xlint:-rawtypes" << "-Xlint:-processing" << "-parameters"
		}
	}
}

subprojects {
	buildscript {
		repositories {
			jcenter()
			mavenCentral()
			maven {
				name = "forge"
				url = "https://files.minecraftforge.net/maven"
			}
		}
		dependencies {
			classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
			classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
		}
	}

	apply plugin: 'java'

	sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

	configurations {
		integrationMod
		compileOnly.extendsFrom integrationMod
		testCompile.extendsFrom integrationMod
		testCompile.extendsFrom forgeGradleGradleStart
	}

	repositories {
		jcenter()
		mavenCentral()

		maven { url 'http://chickenbones.net/maven/' } // CBMP
		maven { url 'http://dvs1.progwml6.com/files/maven/' } // JEI + Tinker's
		maven { url 'http://dyonovan.com/maven2/' } // Industrial Foregoing
		maven { url 'http://maven.ic2.player.to/' } // IC2 + Forestry
		maven { url 'https://dl.bintray.com/squiddev/maven/' } // ConfigGen (why do I use this still?)
		maven { url 'https://maven.amadornes.com/' } // Multipart
		maven { url 'https://maven.blamejared.com/' } // Botania
		maven { url 'https://maven.cil.li/' } // OpenComputers
		maven { url 'https://maven.covers1624.net/' } // CoFH
		maven { url 'https://maven.k-4u.nl/' } // McJty's mods
		maven { url 'https://maven.mcmoddev.com/' } // Tesla
		maven { url 'https://maven.shadowfacts.net' } // Forgelin
		maven { url 'https://mod-buildcraft.com/maven/' } // Buildcraft
		maven { url 'https://maven.hellfiredev.net/' } // Astral Sorcery
		maven { url 'https://squiddev.cc/maven/' } // CC: Tweaked
		maven { url 'https://minecraft.curseforge.com/api/maven/' } // Everything else
	}
}
