plugins {
	id 'com.matthewprenger.cursegradle' version '1.2.0'
	id "com.github.ben-manes.versions" version "0.21.0" // Provides dependencyUpdates task.
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'com.github.johnrengelman.shadow'

archivesBaseName = "plethora"

minecraft {
	mappings channel: project.mappings_channel, version: project.mappings_version

	runs {
		client {
			workingDirectory project.file('run')
			property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
			property 'forge.logging.console.level', 'debug'
			// Added for mods that have per-player data, e.g. Astral Sorcery
			args '--username', 'PlethoraDev'

			mods {
				plethora {
					source sourceSets.main
				}
			}
		}

		server {
			workingDirectory project.file('run')
			property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
			property 'forge.logging.console.level', 'debug'

			mods {
				plethora {
					source sourceSets.main
				}
			}
		}

		testServer {
			parent server
			workingDirectory project.file('test-files/server')
			main 'org.squiddev.plethora.boostrap.LaunchServer'
			property 'fml.queryResult', 'confirm'

			mods {
				plethora {
					source sourceSets.main
					source sourceSets.test
				}
			}
		}
	}
}

dependencies {
	minecraft "net.minecraftforge:forge:${project.mc_version}-${project.forge_version}"
	compile project(':plethora-common')
	project.enabled_integration.each{ integration ->
		compile integration
	}

	// Maven
	implementation fg.deobf("org.squiddev:cc-tweaked-${project.mc_version}:${project.cct_version}")

	testCompile 'junit:junit:4.11'
}

shadowJar {
	classifier = ""
	dependencies {
		include(project(':plethora-common'))
		project.enabled_integration.each{ integration ->
			include(project(integration.path))
		}
	}
}

reobf {
	shadowJar {}
}

build.dependsOn(reobfShadowJar)

curseforge {
	apiKey = project.hasProperty('curseForgeApiKey') ? project.curseForgeApiKey : ''

	project {
		id = '248425'
		releaseType = 'release'
		changelog = "Release notes can be found on the GitHub repository (https://github.com/SquidDev-CC/plethora/releases/tag/v${project.version})."

		mainArtifact reobfShadowJar

		relations {
			requiredLibrary 'cc-tweaked'
		}
	}
}
